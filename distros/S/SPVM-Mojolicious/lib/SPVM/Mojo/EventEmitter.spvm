# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojo::EventEmitter {
  version_from Mojolicious;
  
  use Mojo::Callback;
  
  # Undocumeted Fields
  has events : Hash of Mojo::Callback[];
  
  # Instance Methods
  method catch : void ($cb : Mojo::Callback) {
    $self->on(error => $cb);
  }
  
  method emit : void ($name : string, $arg1 : object = undef, $arg2 : object = undef, $arg3 : object = undef) {
    
    my $events = $self->events;
    
    if (my $cbs = $events->{$name}) {
      for (my $i = 0; $i < @$cbs; $i++) {
        my $cb = (Mojo::Callback)$cbs->[$i];
        
        $cb->($self, $arg1, $arg2, $arg3);
      }
    }
    else {
      if ($name eq "error") {
        die "emit error";
      }
    }
  }
  
  method has_subscribers : int ($name : string) {
    
    my $events = $self->events;
    
    return !!$events->get($name);
  }
  
  method on : void ($name : string, $cb : Mojo::Callback) {
    
    my $events = $self->events;
    
    my $cbs = $events->{$name};
    
    unless ($cbs) {
      $cbs = new Mojo::Callback[0];
      $events->set($name, $cbs);
    }
    
    List->new_ref($cbs)->push($cb);
  }
  
  method once : Mojo::Callback ($name : string, $cb : Mojo::Callback) {
    
    my $wrapper = [$that : Mojo::EventEmitter = $self, $cb : Mojo::Callback] method : void ($name : string) {
      $that->unsubscribe($name => $self);
      $cb->($that);
    };
    weaken $wrapper->{that};
    
    $self->on($name => $wrapper);
    
    return $wrapper;
  }
  
  method subscribers : Mojo::Callback[] ($name : string) {
    
    my $events = $self->events;
    
    my $cbs = $events->{$name};
    
    unless ($cbs) {
      my $new_cbs = new Mojo::Callback[0];
      $events->set($name => $new_cbs);
      $cbs = $new_cbs;
    }
    
    return $cbs;
  }
  
  method unsubscribe : void ($name : string, $cb : Mojo::Callback = undef) {
    
    my $events = $self->events;
    
    # One
    if ($cb) {
      my $cbs = $events->{$name};
      
      if ($cbs) {
        for (my $i = 0; $i < @$cbs; $i++) {
          my $cur_cb = $cbs->[$i];
          if ($cb == $cur_cb) {
            List->new_ref($cbs)->remove($i);
            last;
          }
        }
      }
    }
    
    # All
    else {
      $events->delete($name);
    }
  }
  
  private method events : Hash of Mojo::Callback[] () {
    
    unless ($self->{events}) {
      $self->{events} = Hash->new;
    }
    
    return $self->{events};
  }
  
}

